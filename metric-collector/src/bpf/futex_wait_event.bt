#!/home/anon/.local/bin/bpftrace
/* Signals when a thread enters a waiting state for a futex, and when it
 * returns from waiting. 
 */

#include <linux/futex.h>


BEGIN 
/ str($2) == "debug" /
{
    @pids[$1] = 1;
}


tracepoint:syscalls:sys_enter_access
/ pid == $1 /
{
    $filename = str(args->filename);
    if ($filename != "metric-collector-new-pid") {
        return;
    }

    $new_pid = args->mode;
    if (!@pids[$new_pid]) {
        @pids[$new_pid] = 1;
    }
}

tracepoint:syscalls:sys_enter_futex 
{
    /* We want to account for all waits */
    $task = curtask->group_leader;
    $uaddr = args->uaddr;
    $root_pid = 0;
    $i = 0;
    while(($root_pid == 0) && ($i < 100)) {
        if(!(($task->flags & PF_FORKNOEXEC) && ($task->parent->pid != 1))) {
            $root_pid = $task->pid;
            break;
        }

        $task = $task->parent;
        $i += 1;
    }
    
    if ($root_pid == 0) {
        return;
    }

    if (@futex[$root_pid, $uaddr] && !@pids[pid]) {
        printf("%-15s\t%s\t%lld\n", "NewProcess", comm, pid);
        @pids[pid] = 1;
    }

    if (!@pids[pid]) {
        return;
    }

    if (!@futex[$root_pid, $uaddr]) {
        @futex[$root_pid, $uaddr] = 1;
    }
    
    $op = args->op&(~FUTEX_PRIVATE_FLAG);
    $op = $op&(~FUTEX_CLOCK_REALTIME);
    if (($op == FUTEX_WAIT_BITSET) || ($op == FUTEX_WAIT)) {
        @start[tid] = (nsecs, $root_pid, $uaddr);
        printf("%-15s\t%20lld\t%lld\t%lx\t%20lld\n", 
               "WaitStart", tid, $root_pid, $uaddr, @start[tid].0);
    } else if (($op == FUTEX_WAKE) || ($op == FUTEX_WAKE_BITSET)) {
        @wake[tid] = (nsecs, $root_pid, $uaddr);
    } else {
        printf("%-15s\t%d\n", "UnhandledOpcode", args->op);
    }
}

tracepoint:syscalls:sys_exit_futex 
{
    if (@start[tid].0) {
        $data = @start[tid];
        $since_boot = nsecs;
        $diff = (uint64)($since_boot - $data.0);
        printf("%-15s\t%20lld\t%lld\t%lx\t%20lld\t%lld\t%d\n", 
               "WaitElapsed", tid, $data.1, $data.2, $since_boot, $diff, args->ret);
        delete(@start[tid]);
    }

    if (@wake[tid].0) {
        $data = @wake[tid];
        printf("%-15s\t%20lld\t%lld\t%lx\t%20lld\t%d\n", 
               "Wake", tid, $data.1, $data.2, $data.0, args->ret);
        delete(@wake[tid]);
    }
}

interval:s:1
{
    printf("%-15s\t%20lld\n", "SampleInstant", nsecs);
}

END {
    clear(@start);
    clear(@pids);
    clear(@futex);
    clear(@wake);
}
